# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=rezonateur
pkgver=0.1.0
pkgrel=3
pkgdesc='A virtual-analog 3-band resonator effect LV2/VST2 plugin and JACK client'
arch=(aarch64 x86_64)
url='https://github.com/jpcima/rezonateur'
license=(Boost)
depends=(cairo gcc-libs)
makedepends=(jack mesa)
groups=(pro-audio lv2-plugins vst-plugins)
optdepends=(
  'jack: for running the JACK stand-alone application'
  'lv2-host: for loading the LV2 plugin'
  'vst-host: for loading the VST2 plugin'
)
source=("https://github.com/jpcima/rezonateur/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        'rezonateur-include-stdexcept.diff::https://github.com/jpcima/rezonateur/commit/58ccec5483198cb973be01d4151648b4b70db222.diff'
        'rezonateur-no-opengl-build.diff::https://github.com/jpcima/rezonateur/commit/82b731344a961e210c27107786d0c7a4787f77cc.diff')
sha256sums=('466624147a22299c045e94b385b4f615f16dc91fcb208494c420bedb8e64fac9'
            '03f5b285b49c7753d8fce2e0efef33e834ca118056cbb9e80244091ff5c05b9c'
            'cf7bfd39a74d0ccff9edef268a916b1ad3d2aca9c80f47aaecc0b2fb0f5955c6')

prepare() {
  cd $pkgname-$pkgver
  # fix for missing include with gcc versions
  patch -N -r - -p 1 -i "$srcdir"/rezonateur-include-stdexcept.diff
  # don't build unneeded DPF/DGL OpenGL version
  patch -N -r - -p 1 -i "$srcdir"/rezonateur-no-opengl-build.diff
  cd dpf
  # use included patch to fix bypass param in bundled DPF version
  patch -N -r - -p 1 -i ../resources/patch/DPF-bypass.patch || return 0
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  # LV2 bundles
  install -Dm644 bin/$pkgname.lv2/*.ttl -t \
    "$pkgdir"/usr/lib/lv2/$pkgname.lv2
  install -Dm755 bin/$pkgname.lv2/*.so -t \
    "$pkgdir"/usr/lib/lv2/$pkgname.lv2
  install -Dm644 bin/$pkgname-stereo.lv2/*.ttl -t \
    "$pkgdir"/usr/lib/lv2/$pkgname-stereo.lv2
  install -Dm755 bin/$pkgname-stereo.lv2/*.so -t \
    "$pkgdir"/usr/lib/lv2/$pkgname-stereo.lv2
  # VST2 shared libraries
  install -Dm755 bin/$pkgname{-stereo,}-vst.so -t "$pkgdir"/usr/lib/vst
  # Stand-alone JACK client binaries
  install -Dm755 bin/$pkgname{-stereo,} -t "$pkgdir"/usr/bin
}
