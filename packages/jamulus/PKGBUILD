# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Julien Taverna <jujudusud gmail com>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgbase=jamulus
pkgname=(jamulus jamulus-headless)
pkgver=3.8.2
pkgrel=2
pkgdesc="Internet jam session software"
arch=(x86_64 aarch64)
url='https://jamulus.io/'
license=(GPL2 BSD custom:STK)
depends=(gcc-libs qt5-base)
provides=(jamulus-server)
conflicts=(jamulus-git)
makedepends=(jack)
groups=(pro-audio)
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jamulussoftware/${pkgbase}/archive/r${pkgver//./_}.tar.gz"
'jamulus-server.service'
'jamulus.sysusers'
'override.conf')
backup=('etc/systemd/system/jamulus-server.service.d/override.conf')
sha512sums=('8630cbcbfbdc2051ecbbd5158211efce2f2e47ae18413cb7e549bbe8909110ff13ab8ac2dbacd8c41a88b38d9988ea7c3955a6fec25a1eb18170ab6d792063c7'
            '61a1ab8e5b6442ecc27935b42b6f81e694e4623ccef2d4f43723b5858a8d54492dab5f73fcf8a6d23bd09e15b3991744a95b042009bff7160e6a5b12c36af5c2'
            'a5863757f935718d0ea90940b0d3406be2111a727ce936c67390c09aa78f6f5925c0bbca1d9d5f9d2c1f74b761d5da43ad00b0684f464bcb3e6c875bf2791e70'
            '9a494c11fb999f59bf00033ae3968a2e613529f1337c3189990e2fe6215d5b0830005ac5e1378304d868bd58e4fc78d17a03ab7b880b5efe8e448c4addef3ac3')
_pkgsrc=${pkgbase}-r${pkgver//./_}

build() {
  cd ${_pkgsrc}
  qmake "CONFIG+=nosound headless" TARGET=jamulus-headless
  make clean
  make
  qmake "CONFIG+=noupcasename"
  make clean
  make
}

package_jamulus() {
  cd ${_pkgsrc}
  pkgdesc+=" (client and server)"
  depends+=(libjack.so)
  install -vDm755 jamulus -t "$pkgdir"/usr/bin
  install -vDm644 ChangeLog README.md -t "$pkgdir"/usr/share/doc/${pkgbase}
  install -vDm644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgbase}
  install -vDm644 distributions/jamulus{,-server}.desktop -t "$pkgdir"/usr/share/applications
  install -vDm644 distributions/jamulus{,-server}.svg -t "$pkgdir"/usr/share/pixmaps
  install -vDm644 ../jamulus-server.service -t "$pkgdir"/usr/lib/systemd/system
  install -vDm644 ../jamulus.sysusers "$pkgdir"/usr/lib/sysusers.d/${pkgbase}.conf
  # user can edit this using `systemctl edit jamulus-server`
  install -vDm644 ../override.conf -t "$pkgdir"/etc/systemd/system/jamulus-server.service.d
}

package_jamulus-headless() {
  cd ${_pkgsrc}
  pkgdesc+=" (headless server)"
  conflicts+=(jamulus)
  install -vDm755 jamulus-headless "$pkgdir"/usr/bin/jamulus
  install -vDm644 ChangeLog README.md -t "$pkgdir"/usr/share/doc/${pkgbase}
  install -vDm644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgbase}
  install -vDm644 ../jamulus-server.service -t "$pkgdir"/usr/lib/systemd/system
  install -vDm644 ../jamulus.sysusers "$pkgdir"/usr/lib/sysusers.d/${pkgbase}.conf
  # user can edit this using `systemctl edit jamulus-server`
  install -vDm644 ../override.conf -t "$pkgdir"/etc/systemd/system/jamulus-server.service.d
}
