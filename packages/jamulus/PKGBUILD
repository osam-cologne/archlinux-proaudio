# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Julien Taverna <jujudusud gmail com>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgbase=jamulus
pkgname=(jamulus jamulus-server)
pkgver=3.8.2
pkgrel=2
pkgdesc="Internet jam session software"
arch=(x86_64 aarch64)
url='https://jamulus.io/'
license=(GPL2)
depends=(glibc gcc-libs qt5-base)
provides=(jamulus-server)
conflicts=(jamulus-git)
makedepends=(jack)
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jamulussoftware/${pkgbase}/archive/r${pkgver//./_}.tar.gz")
sha512sums=('8630cbcbfbdc2051ecbbd5158211efce2f2e47ae18413cb7e549bbe8909110ff13ab8ac2dbacd8c41a88b38d9988ea7c3955a6fec25a1eb18170ab6d792063c7')

build() {
  cd "${srcdir}/${pkgbase}-r${pkgver//./_}"
  qmake "CONFIG+=nosound headless" TARGET=jamulus-server
  make
  qmake "CONFIG+=noupcasename"
  make
}

package_jamulus() {
  cd "${srcdir}/${pkgbase}-r${pkgver//./_}"
  pkgdesc+=" (client and server)"
  depends+=(libjack.so)
  provides+=(jamulus)
  install -Dm755 jamulus "${pkgdir}/usr/bin/jamulus"
  install -vDm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgbase}"
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgbase}"
  install -Dm644 distributions/jamulus.desktop "${pkgdir}/usr/share/applications/jamulus.desktop"
  install -Dm644 distributions/jamulus.svg "${pkgdir}/usr/share/pixmaps/jamulus.svg"
  install -Dm644 distributions/jamulus-server.desktop "${pkgdir}/usr/share/applications/jamulus-server.desktop"
  install -Dm644 distributions/jamulus-server.svg "${pkgdir}/usr/share/pixmaps/jamulus-server.svg"
}

package_jamulus-server() {
  cd "${srcdir}/${pkgbase}-r${pkgver//./_}"
  pkgdesc+=" (headless server)"
  conflicts+=(jamulus)
  install -Dm755 jamulus-server "${pkgdir}/usr/bin/jamulus"
  install -vDm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgbase}"
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgbase}"
}
