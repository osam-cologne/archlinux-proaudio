# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Julien Taverna <jujudusud gmail com>

pkgname=jamulus
_pkgname=Jamulus
pkgver=3.8.2
pkgrel=2
pkgdesc="Internet jam session software"
arch=('x86_64')
url='https://jamulus.io/'
license=('GPL2')
depends=('glibc' 'gcc-libs' 'qt5-base' 'qt5-translations')
conflicts=('jamulus-git')
makedepends=('jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jamulussoftware/jamulus/archive/r${pkgver//./_}.tar.gz")
sha512sums=('8630cbcbfbdc2051ecbbd5158211efce2f2e47ae18413cb7e549bbe8909110ff13ab8ac2dbacd8c41a88b38d9988ea7c3955a6fec25a1eb18170ab6d792063c7')

build() {
  cd "${srcdir}/${pkgname}-r${pkgver//./_}"
  qmake
  make
}

package() {
  depends+=('libjack.so')
  cd "${srcdir}/${pkgname}-r${pkgver//./_}"
  install -Dm755 Jamulus "${pkgdir}/usr/bin/Jamulus"
  install -Dm644 distributions/jamulus.desktop "${pkgdir}/usr/share/applications/jamulus.desktop"
  install -Dm644 distributions/jamulus.svg "${pkgdir}/usr/share/pixmaps/jamulus.svg"
  install -Dm644 distributions/jamulus-server.desktop "${pkgdir}/usr/share/applications/jamulus-server.desktop"
  install -Dm644 distributions/jamulus-server.svg "${pkgdir}/usr/share/pixmaps/jamulus-server.svg"
  install -vDm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
