# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: Milkii Brewster <milkii on Freenode IRC>

_pkgname=ssr
pkgname="$_pkgname-git"
pkgdesc="A sympathetic string resonator plugin and JACK application (git version)."
pkgver=r10.e1999da
pkgrel=1
arch=(x86_64)
url="https://github.com/jpcima/ssr"
license=(MIT)
groups=(lv2-plugins pro-audio vst-plugins)
depends=(cairo)
makedepends=(git mesa jack)
optdepends=('jack: stand-alone JACK application')
provides=("$_pkgname" "$_pkgname.lv2")
conflicts=("$_pkgname" "$_pkgname.lv2" "$_pkgname.lv2-git")
source=("$_pkgname::git+https://github.com/jpcima/ssr"
        'dpf::git+https://github.com/DISTRHO/DPF.git'
)
sha256sums=(SKIP SKIP)

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $_pkgname
  git submodule init
  git config submodule.dpf.url "$srcdir/dpf"
  git submodule update
}

build() {
  cd $_pkgname
  make PREFIX=/usr BUILD_VST2=true
}

package() {
  depends+=(libjack.so)
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir/" BUILD_VST2=true install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}
}
