# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Phil A. <flying-sheep@web.de>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Adrain Sinclair <adrian at adrusi dot com>
# Contributor: Miroslav Koškár <http://mkoskar.com/>
# Contributor: Florian Hülsmann <fh@cbix.de>

pkgname=vcvrack
pkgver=2.1.0
pkgrel=2
pkgdesc='Open-source Eurorack modular synthesizer simulator'
url='https://vcvrack.com/'
license=(custom CCPL GPL3)
arch=(x86_64)
_plugin_name=Fundamental
_plugin_ver=2.0.3
_plugin_pkg=${pkgname}-${_plugin_name,,}
depends=(
  curl
  glew
  # does not start with glfw-wayland
  glfw-x11
  jack
  jansson
  libarchive
  libpulse
  openssl
  rtaudio
  rtmidi
  speexdsp
  zenity
  zstd
)
makedepends=(git gendesk jq)
provides=($_plugin_pkg)
conflicts=($_plugin_pkg)
groups=(pro-audio)
source=(
  "${pkgname}-${pkgver}::git+https://github.com/vcvrack/rack.git#tag=v$pkgver"
  'git+https://github.com/VCVRack/nanovg.git'
  'git+https://github.com/memononen/nanosvg.git'
  'git+https://github.com/AndrewBelt/osdialog.git'
  'git+https://github.com/VCVRack/oui-blendish.git'
  'git+https://bitbucket.org/j_norberg/fuzzysearchdatabase.git'
  'git+https://github.com/gulrak/filesystem.git'
  'git+https://bitbucket.org/jpommier/pffft.git'
  "${_plugin_pkg}-${_plugin_ver}.tar.gz::https://github.com/VCVRack/${_plugin_name}/archive/v${_plugin_ver}.tar.gz"
  'system-plugins.patch'
  'vcvrack.sh'
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '285b7e014ff40f214d866487d70f5e1f69c9e8b4c14727be0635451598d107cde9bb24e6f08d2e307b629d28851ee6b86e6e7c630a838359385f96280e249c03'
            '86196f0aed29cf35753bc27f94b3234c6f7131d6c248bee69e31e61ac352ad1596a007fe32f55b2a3173feab24e5b2664dbe7deee7fa607a1974792a3c5450d7'
            '8ca3fbc790fa5d311ce41b5b9e84a79fe51db73cbf0f56f04d0429b28f0c4c0646e2d0c081b584161356ec24b06cc0181b115b865d564ff1452c64a57135acf6')
_submodule_deps=(nanovg nanosvg osdialog oui-blendish fuzzysearchdatabase filesystem pffft)

prepare() {
  cd "${pkgname}-$pkgver"
  for _mod in "${_submodule_deps[@]}"; do
    git submodule init "dep/$_mod"
    git config "submodule.dep/${_mod}.url" "${srcdir}/$_mod"
    git submodule update "dep/$_mod"
  done

  # fix plugin.mk’s use of jq, if SLUG/VERSION are unset
  sed -e 's/SLUG :=/SLUG ?=/' \
    -e 's/VERSION :=/VERSION ?=/' \
    -i plugin.mk

  # add target to only build included dependencies
  echo 'includes: $(nanovg) $(nanosvg) $(osdialog) $(oui-blendish) $(fuzzysearchdatabase) $(ghcfilesystem) $(pffft)' >> dep/Makefile

  # recent changes to rtaudio.cpp require an unrelease version of rtaudio
  git checkout 6ae7fe05216950e461a27e05e8b9de53ccf8247b src/rtaudio.cpp

  # load system-wide before user-installed plugins
  patch -p1 -i ../system-plugins.patch

  gendesk -f -n \
    --pkgname $pkgname \
    --name "VCV Rack" \
    --exec Rack \
    --pkgdesc "$pkgdesc" \
    --genericname "Virtual modular synthesizer" \
    --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  _ldflags="-shared -ldl \
    $(pkg-config --libs glew \
    glfw3 jansson libcurl openssl \
    libarchive libzstd speexdsp \
    samplerate rtmidi rtaudio gl \
    alsa jack libpulse libpulse-simple)"
  VERSION=$pkgver make -C dep includes
  VERSION=$pkgver make LDFLAGS+="$_ldflags" STANDALONE_LDFLAGS=""
  cd ../${_plugin_name}-$_plugin_ver
  VERSION=$_plugin_ver RACK_DIR="$srcdir"/${pkgname}-$pkgver make dist LDFLAGS+="$_ldflags"
}

package() {
  cd "${pkgname}-$pkgver"
  install -vDm755 Rack -t "$pkgdir"/opt/$pkgname
  install -vDm755 libRack.so -t "$pkgdir"/opt/$pkgname
  install -vDm755 "$srcdir"/vcvrack.sh "$pkgdir"/usr/bin/Rack
  install -vDm644 -t "$pkgdir"/opt/$pkgname template.vcv
  install -vDm644 -t "$pkgdir"/opt/$pkgname Core.json
  install -vDm644 -t "$pkgdir"/opt/$pkgname cacert.pem

  # resources
  cp -dr --preserve=mode res -t "$pkgdir"/opt/$pkgname

  # headers (required for plugins)
  for _path in {app,dsp,engine,plugin,simd,ui,widget,window}; do
    install -vDm644 include/${_path}/* \
      -t "$pkgdir"/usr/include/${pkgname}/${_path}/
  done
  install -vDm644 include/*.{h,hpp} -t "$pkgdir"/usr/include/${pkgname}/
  install -vDm644 dep/include/*.h -t "$pkgdir"/usr/include/${pkgname}/dep
  # Makefile snippets required for plugins
  install -vDm644 {arch,compile,dep,plugin}.mk -t "$pkgdir"/usr/share/$pkgname

  # xdg desktop integration
  install -vDm644 ${pkgname}.desktop -t "$pkgdir"/usr/share/applications/
  install -vDm644 res/icon.png "$pkgdir"/usr/share/pixmaps/${pkgname}.png
  # licenses
  install -vDm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname

  # Fundamental plugin
  cd ../${_plugin_name}-$_plugin_ver
  install -d "$pkgdir"/opt/${pkgname}/plugins
  cp -a dist/$_plugin_name -t "$pkgdir"/opt/${pkgname}/plugins/
}
