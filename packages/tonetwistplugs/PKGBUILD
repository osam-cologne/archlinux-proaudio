# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_name=ToneTwistPlugs
pkgname=${_name,,}
pkgver=0.7
pkgrel=1
pkgdesc='A small collection of multi-format, cross-platform guitar pedal effect plugins'
arch=(aarch64 x86_64)
url="https://github.com/brummer10/$_name"
license=(GPL2)
groups=(clap-plugins lv2-plugins pro-audio vst-plugins vst3-plugins)
depends=(cairo glibc gcc-libs libx11 libxcursor libxext libxrandr)
makedepends=(dbus lv2)
checkdepends=(lv2lint)
optdepends=(
  'jack: for using the stand-alone programs with JACK'
  'clap-host: for loading the CLAP format plugins'
  'lv2-host: for loading the LV2 format plugins'
  'vst-host: for loading the VST2 format plugins'
  'vst3-host: for loading the VST3 format plugins'
)
_dpf_commit=df2afed246113e4459808b112583d54cf7e433e9
_pugl_commit=21cb3c8b480f4c97f888e3d08630ded9c8bf04a2
source=(
  "$_name-$pkgver.tar.gz::https://github.com/brummer10/$_name/archive/refs/tags/v$pkgver.tar.gz"
  "dpf-$_dpf_commit.tar.gz::https://github.com/DISTRHO/DPF/archive/$_dpf_commit.tar.gz"
  "pugl-$_pugl_commit.tar.gz::https://github.com/DISTRHO/pugl/archive/$_pugl_commit.tar.gz"
)
sha256sums=('f07d415de898d76ac9c156ceb6e51564a6a8f5068adcb39f32bec85eea06e6f1'
            '4fc868815cfb1027d784cc64fca83a5fd01c7b6f3ac6cc7199ae9708654e1ff2'
            'aca3420ae9db14cacfba21bfb83d7277886f9d3a7117d18611455d4383ad6606')
_plugins=(
  BoobTube
  CollisionDrive
  MetalTone
  Rumor
  TubeScreamer
  ValveCaster
)

prepare() {
  cd $_name-$pkgver
  rm -rf dpf
  ln -s "$srcdir"/DPF-$_dpf_commit dpf
  rm -rf dpf/dgl/src/pugl-upstream
  ln -s "$srcdir"/pugl-$_pugl_commit dpf/dgl/src/pugl-upstream
  touch pugl_patch.flag
  cd dpf/dgl/src/pugl-upstream
  patch -p1 -N -r - -i "$srcdir"/$_name-$pkgver/pugl.patch
}

build() {
  cd $_name-$pkgver
  make SKIP_NATIVE_AUDIO_FALLBACK=true
}

check() {
  cd $_name-$pkgver
  local _plugin_name
  local base_url="urn:brummer10:"
  for _plugin in "${_plugins[@]}"; do
    lv2lint -s lv2_generate_ttl -Mpack -I bin/${_plugin,,}.lv2 "$base_url${_plugin,,}"
  done
}

package() {
  depends+=(libdbus-1.so)
  cd $_name-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
  for _plugin in "${_plugins[@]}"; do
    install -vDm 644 $_plugin.png -t "$pkgdir"/usr/share/doc/$pkgname
  done
}
