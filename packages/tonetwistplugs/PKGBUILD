# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_name=ToneTwistPlugs
pkgname=${_name,,}
pkgver=0.8
pkgrel=1
pkgdesc='A small collection of multi-format, cross-platform guitar pedal effect plugins'
arch=(aarch64 x86_64)
url="https://github.com/brummer10/$_name"
license=(GPL-2.0-or-later)
groups=(clap-plugins lv2-plugins pro-audio vst-plugins vst3-plugins)
depends=(cairo glibc gcc-libs libx11 libxcursor libxext libxrandr)
makedepends=(dbus lv2)
checkdepends=(lv2lint)
optdepends=(
  'jack: for using the stand-alone programs with JACK'
  'clap-host: for loading the CLAP format plugins'
  'lv2-host: for loading the LV2 format plugins'
  'vst-host: for loading the VST2 format plugins'
  'vst3-host: for loading the VST3 format plugins'
)
_dpf_commit=29addec6f6e072162d0269fe8f1341b333c69e42
_pugl_commit=e33b2f6b0cea6d6263990aa9abe6a69fdfba5973
source=(
  "$_name-$pkgver.tar.gz::https://github.com/brummer10/$_name/archive/refs/tags/v$pkgver.tar.gz"
  "dpf-$_dpf_commit.tar.gz::https://github.com/DISTRHO/DPF/archive/$_dpf_commit.tar.gz"
  "pugl-$_pugl_commit.tar.gz::https://github.com/DISTRHO/pugl/archive/$_pugl_commit.tar.gz"
)
sha256sums=('417810b97e7835f49f97fdcf329110da4ee50ac0e9dc0dc5ef8840e0b78a7246'
            '30ecd4b1174d80377a679040900329ce709465406fa86e935e1ff21dba5b1c23'
            '7e813d35d619a0ba3e790be5e102cfd2dc7c1f7b99333c9aa0a8661ca8419e02')
_plugins=(
  BoobTube
  CollisionDrive
  MetalTone
  Rumor
  TubeScreamer
  ValveCaster
)

prepare() {
  cd $_name-$pkgver
  rm -rf dpf
  ln -s "$srcdir"/DPF-$_dpf_commit dpf
  rm -rf dpf/dgl/src/pugl-upstream
  ln -s "$srcdir"/pugl-$_pugl_commit dpf/dgl/src/pugl-upstream
  touch pugl_patch.flag
  cd dpf/dgl/src/pugl-upstream
  patch -p1 -N -r - -i "$srcdir"/$_name-$pkgver/pugl.patch
}

build() {
  cd $_name-$pkgver
  make SKIP_NATIVE_AUDIO_FALLBACK=true BUILD_AU=false
}

check() {
  cd $_name-$pkgver
  local _plugin_name
  local base_url="urn:brummer10:"
  for _plugin in "${_plugins[@]}"; do
    lv2lint -s lv2_generate_ttl -Mpack -I bin/${_plugin,,}.lv2 "$base_url${_plugin,,}"
  done
}

package() {
  depends+=(libdbus-1.so)
  cd $_name-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr BUILD_AU=false install
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
  for _plugin in "${_plugins[@]}"; do
    install -vDm 644 $_plugin.png -t "$pkgdir"/usr/share/doc/$pkgname
  done
}
