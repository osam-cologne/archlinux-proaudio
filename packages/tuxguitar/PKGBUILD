# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Stephan Springer <buzo+arch@Lini.de>
# Contributor: Thanos Apostolou <thanosapostolou@outlook.com>
# Contributor: Alexandre Moine <alexandre@moine.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: |AhIoRoS| < ahioros@gmail.com >
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgbase=tuxguitar
pkgname=(tuxguitar tuxguitar-common tuxguitar-gtk2)
pkgver=1.5.5
pkgrel=2
pkgdesc="Multitrack guitar tablature editor and player"
arch=('x86_64' 'i686' 'armv7h')
_java_ver=11
_swt_rel=4.22-202111241800
_swt_ver=${_swt_rel:0:-13}
url="https://sourceforge.net/projects/tuxguitar/"
license=('LGPL')
depends=('alsa-lib' 'libxtst' 'glibc' "java-runtime-openjdk=${_java_ver}" 'hicolor-icon-theme')
makedepends=('unzip' 'zip' 'ant' 'jack' 'fluidsynth' "java-environment-openjdk=${_java_ver}" 'maven')
optdepends=('fluidsynth')
source=(https://downloads.sourceforge.net/tuxguitar/tuxguitar-$pkgver-src.tar.gz
        https://archive.eclipse.org/eclipse/downloads/drops4/R-${_swt_rel}/swt-${_swt_ver}-gtk-linux-x86_64.zip
        dep-versions.patch
        tuxguitar.sh
        tuxguitar-gtk2.sh)
sha256sums=('2e855500d9cea6987962a851d7e3fcb144e3220b10f71fba1514913e70542398'
            '55b46b575e8c93172be81e096fb72bce83b60df8087b4863a1a057aaead30b1f'
            'f42adf9b3c2df273e192a5a620695bd2deca2fd0afc973cf2981b193413e6ad9'
            '480b0526254131f9bfd9cf0ad5f80e53b9d963294aeabfe72623f72cbce98e5d'
            '3c880fa74fc458dc08bc210416fa6e04930df5c8df6e0c9954d1ab7f5b4541eb')
case $CARCH in
  i686) _arch=x86;;
  armv7h) _arch=armv7hl;;
  *) _arch=$CARCH;;
esac

prepare() {
  export MAVEN_OPTS="$MAVEN_OPTS -Duser.home=$srcdir"
  export PATH="/usr/lib/jvm/java-${_java_ver}-openjdk/bin:$PATH"

  mvn deploy:deploy-file \
    -DgroupId=org.eclipse.swt \
    -DartifactId=org.eclipse.swt.gtk.linux.x86_64 \
    -Dversion=${_swt_ver} \
    -Durl="file://${srcdir}/.m2/repository" \
    -Dfile="${srcdir}/swt.jar" \
    -Dsources="${srcdir}/src.zip" \
    -DupdateReleaseInfo=true

  cd tuxguitar-$pkgver-src
  patch -p1 -i ../dep-versions.patch
}

build() {
  export MAVEN_OPTS="$MAVEN_OPTS -Duser.home=$srcdir"
  export PATH="/usr/lib/jvm/java-${_java_ver}-openjdk/bin:$PATH"
  cd tuxguitar-$pkgver-src

  mvn \
    -P platform-linux-$_arch \
    -Dproject.build.outputTimestamp=$SOURCE_DATE_EPOCH \
    clean install
  for _i in build-scripts/{tuxguitar,native-modules/tuxguitar-{alsa,oss,jack,fluidsynth}}-linux-$_arch
  do (
    cd $_i
    mvn \
      -Dproject.build.outputTimestamp=$SOURCE_DATE_EPOCH \
      clean install
  ); done
}

package_tuxguitar-common () {
  # tuxguitar
  cd tuxguitar-$pkgver-src/build-scripts
  install -d "$pkgdir"/opt
  cp -a tuxguitar-linux-$_arch/target/tuxguitar-$pkgver-linux-$_arch "$pkgdir"/opt/tuxguitar
  cp -a native-modules/tuxguitar-{alsa,oss,jack,fluidsynth}-linux-$_arch/target/build/* \
    "$pkgdir"/opt/tuxguitar/

  # icons
  cd ..
  for _i in 16 24 32 48 64 96; do
    _dir="$pkgdir"/usr/share/icons/hicolor/${_i}x${_i}
    install -d "$_dir"/{apps,mimetypes}
    install -m644 TuxGuitar/share/skins/Lavender/icon-${_i}x${_i}.png "$_dir"/apps/tuxguitar.png
    for _m in audio-x-{tuxguitar,gtp,ptb}; do
      ln -sr "$_dir"/apps/tuxguitar.png "$_dir"/mimetypes/$_m.png
    done
  done 
  
  # misc
  cd "${srcdir}/tuxguitar-$pkgver-src/misc"
  install -Dm644 tuxguitar.xml "$pkgdir"/usr/share/mime/packages/tuxguitar.xml
  install -Dm644 tuxguitar.desktop "$pkgdir"/usr/share/applications/tuxguitar.desktop
  
  # remove tuxguitar.sh
  rm "${pkgdir}/opt/tuxguitar/tuxguitar.sh"
}

package_tuxguitar () {
  depends=('tuxguitar-common' 'gtk3')
  replaces=('tuxguitar-gtk3')
  arch=('any')
  
  install -Dm 755 tuxguitar.sh "$pkgdir"/usr/bin/tuxguitar
}

package_tuxguitar-gtk2 () {
  depends=('tuxguitar-common' 'gtk2')
  conflicts=('tuxguitar')
  provides=('tuxguitar')
  arch=('any')
  
  install -Dm 755 tuxguitar-gtk2.sh "$pkgdir"/usr/bin/tuxguitar
}
