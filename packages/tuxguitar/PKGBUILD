# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Stephan Springer <buzo+arch@Lini.de>
# Contributor: Thanos Apostolou <thanosapostolou@outlook.com>
# Contributor: Alexandre Moine <alexandre@moine.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: |AhIoRoS| < ahioros@gmail.com >
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgbase=tuxguitar
pkgname=tuxguitar
pkgver=1.5.5
pkgrel=2
pkgdesc='Multitrack guitar tablature editor and player'
arch=(x86_64 i686 armv7h)
_java_ver=11
_swt_rel='4.22-202111241800'
_swt_ver=${_swt_rel:0:-13}
url='https://sourceforge.net/projects/tuxguitar/'
license=(LGPL)
groups=(pro-audio)
depends=()
makedepends=(alsa-lib fluidsynth jack "java-environment-openjdk=${_java_ver}" maven)
optdepends=('alsa-lib: playback with ALSA'
            'fluidsynth: alternative synthesizer for playback'
            'jack: playback and MIDI support with JACK'
            'pulseaudio: playback with PulseAudio')
conflicts=(tuxguitar-common tuxguitar-gtk2)
source=("https://downloads.sourceforge.net/tuxguitar/tuxguitar-${pkgver}-src.tar.gz"
        "https://archive.eclipse.org/eclipse/downloads/drops4/R-${_swt_rel}/swt-${_swt_ver}-gtk-linux-x86_64.zip"
        'dep-versions.patch'
        'tuxguitar.sh')
sha256sums=('2e855500d9cea6987962a851d7e3fcb144e3220b10f71fba1514913e70542398'
            '55b46b575e8c93172be81e096fb72bce83b60df8087b4863a1a057aaead30b1f'
            'f42adf9b3c2df273e192a5a620695bd2deca2fd0afc973cf2981b193413e6ad9'
            'ddaed0fc471de5dbee24b00c92f63cdee505ab6a2dd07b2e253d1a65eaa44b6e')
# project internal build target names
case $CARCH in
  i686) _arch=x86;;
  armv7h) _arch=armv7hl;;
  *) _arch=$CARCH;;
esac
_javapath="/usr/lib/jvm/java-${_java_ver}-openjdk/bin:$PATH"

prepare() {
  cd tuxguitar-$pkgver-src

  # locally install recent SWT version to be included in the package
  MAVEN_OPTS="$MAVEN_OPTS -Duser.home=$srcdir" \
  PATH="$_javapath" \
  mvn install:install-file \
    -Dfile="${srcdir}/swt.jar" \
    -DgroupId=org.eclipse.swt \
    -DartifactId=org.eclipse.swt.gtk.linux.x86_64 \
    -Dversion=${_swt_ver} \
    -Dpackaging=jar

  # https://sourceforge.net/p/tuxguitar/feature-requests/96/
  # https://sourceforge.net/p/tuxguitar/patches/45/
  # Reproducible build; recent SWT version; use https for swt-repo
  patch -p1 -i ../dep-versions.patch

  # https://sourceforge.net/p/tuxguitar/support-requests/13/
  rm TuxGuitar-resources/resources/soundfont/*
}

build() {
  cd tuxguitar-$pkgver-src

  MAVEN_OPTS="$MAVEN_OPTS -Duser.home=$srcdir" \
  PATH="$_javapath" \
  mvn \
    -P platform-linux-$_arch \
    -Dproject.build.outputTimestamp=$SOURCE_DATE_EPOCH \
    clean install
  for _module in build-scripts/{tuxguitar,native-modules/tuxguitar-{alsa,fluidsynth,jack}}-linux-$_arch
  do (
    cd $_module
    MAVEN_OPTS="$MAVEN_OPTS -Duser.home=$srcdir" \
    PATH="$_javapath" \
    mvn \
      -Dproject.build.outputTimestamp=$SOURCE_DATE_EPOCH \
      clean install
  ); done
}

package() {
  depends+=(libxtst glibc "java-runtime-openjdk=${_java_ver}" hicolor-icon-theme gtk3)
  # base libraries
  cd "$srcdir"/tuxguitar-${pkgver}-src/build-scripts/tuxguitar-linux-${_arch}/target/tuxguitar-${pkgver}-linux-${_arch}
  install -Dm644 lib/*.jar -t "$pkgdir"/usr/share/java/$pkgname
  install -Dm755 lib/*.so -t "$pkgdir"/usr/lib/$pkgname
  # plugins
  install -Dm644 share/plugins/*.jar -t "$pkgdir"/usr/share/java/$pkgname
  cd "$srcdir"/tuxguitar-${pkgver}-src/build-scripts/native-modules
  install -Dm644 tuxguitar-{alsa,fluidsynth,jack}-linux-${_arch}/target/build/share/plugins/*.jar \
    -t "$pkgdir"/usr/share/java/$pkgname
  install -Dm755 tuxguitar-{alsa,fluidsynth,jack}-linux-${_arch}/target/build/lib/*.so \
    -t "$pkgdir"/usr/lib/$pkgname

  # Application data and docs
  cd "$srcdir"/tuxguitar-${pkgver}-src/build-scripts/tuxguitar-linux-${_arch}/target/tuxguitar-${pkgver}-linux-${_arch}
  install -d "$pkgdir"/usr/share/{,doc/}$pkgname
  cp -a {dist,doc,share} "${pkgdir}"/usr/share/${pkgname}/
  rm -r "${pkgdir}"/usr/share/${pkgname}/share/{help,plugins,soundfont,lang/toutf.pl}
  cp -a share/help "${pkgdir}"/usr/share/doc/$pkgname
  # ensure correct permissions for data
  find "$pkgdir"/usr/share/{,doc/}$pkgname -type f -exec chmod 644 {} \;

  # icons
  cd "$srcdir"/tuxguitar-${pkgver}-src
  for _size in 16x16 24x24 32x32 48x48 64x64 96x96; do
    _dir="$pkgdir"/usr/share/icons/hicolor/$_size
    install -d "$_dir"/{apps,mimetypes}
    install -m644 TuxGuitar/share/skins/Lavender/icon-${_size}.png "$_dir"/apps/tuxguitar.png
    for _mime in audio-x-{tuxguitar,gtp,ptb}; do
      ln -sr "${_dir}"/apps/tuxguitar.png "$_dir"/mimetypes/${_mime}.png
    done
  done 
  
  # mimetype and launcher
  cd "$srcdir"/tuxguitar-${pkgver}-src/misc
  install -Dm644 tuxguitar.xml "$pkgdir"/usr/share/mime/packages/tuxguitar.xml
  install -Dm644 tuxguitar.desktop "$pkgdir"/usr/share/applications/tuxguitar.desktop
  
  # use custom instead of upstream starter script
  install -Dm755 "$srcdir"/tuxguitar.sh "$pkgdir"/usr/bin/tuxguitar
}
