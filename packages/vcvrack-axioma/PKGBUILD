# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

_slug=Axioma
_name=Axioma
pkgname=vcvrack-axioma
pkgver=2.0.0
pkgrel=1
pkgdesc='Axioma VCV Rack modules'
arch=(x86_64 aarch64)
url='https://github.com/kauewerner/Axioma'
license=(GPL3)
groups=(proaudio vcvrack-plugins)
depends=(gcc-libs vcvrack)
makedepends=(simde zstd)
source=("$pkgname-$pkgver.tar.gz::https://github.com/kauewerner/$_name/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('59af94b6f58788e1c15e28e028d569d35452b55687acc6c419d928a9fe07a634')

build() {
  cd $_name-$pkgver
  make SLUG=$_slug VERSION=$pkgver RACK_DIR=/usr/share/vcvrack dist
}

package() {
  cd $_name-$pkgver
  install -d "$pkgdir"/usr/lib/vcvrack/plugins
  cp -va dist/$_slug -t "$pkgdir"/usr/lib/vcvrack/plugins
  # remove common license
  rm "$pkgdir"/usr/lib/vcvrack/plugins/$_slug/LICENSE
}
