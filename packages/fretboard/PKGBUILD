# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgname=fretboard
pkgver=7.0
pkgrel=1
pkgdesc='Simple guitar chord lookup tool'
arch=(aarch64 x86_64)
url='https://apps.gnome.org/Fretboard/'
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(gtk4)
makedepends=(blueprint-compiler git libadwaita meson rust)
source=("git+https://github.com/bragefuglseth/$pkgname#tag=v$pkgver-fixed")
sha256sums=('1125cbfc343c05a1cd2ca91c6cbeda6cb2e3f237e77ee841f500fe8c252b65e4')

build() {
  export RUSTUP_TOOLCHAIN=stable
  # https://github.com/bragefuglseth/fretboard/issues/51
  arch-meson --reconfigure -Dsandboxed=true -Dprofile=development $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libadwaita-1.so)
  meson install -C build --destdir "$pkgdir"
}
