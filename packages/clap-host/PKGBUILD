# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgname=clap-host
pkgver=1.0.2
pkgrel=1
pkgdesc='CLAP example host'
arch=(aarch64 x86_64)
url='https://github.com/free-audio/clap-host'
license=(MIT)
depends=(qt6-base)
makedepends=(catch2 cmake rtaudio rtmidi)
groups=(pro-audio)
_clap_ref='27b7af0'
_clap_helpers_ref='4d4e297'
source=("$pkgname-$pkgver.tar.gz::https://github.com/free-audio/$pkgname/archive/refs/tags/$pkgver.tar.gz"
        "clap-$_clap_ref.tar.gz::https://github.com/free-audio/clap/archive/$_clap_ref.tar.gz"
        "clap-helpers-$_clap_helpers_ref.tar.gz::https://github.com/free-audio/clap-helpers/archive/$_clap_helpers_ref.tar.gz")
sha256sums=('ec4342d8edcdd8262da1042cd6b3862d180dfaf740eb326239b86cbb6331dd4e'
            '8a85ac0fe0ce58c6e641386257a21bd5167b55f05bdd0e69b9617c49b78b9a6b'
            '0681e844336212e56df41b9ed7937b6e0d82f4005209e392fbc991e5d3ed6dd4')
noextract=(clap-$_clap_ref.tar.gz clap-helpers-$_clap_helpers_ref.tar.gz)

prepare() {
  cd $pkgname-$pkgver
  bsdtar -xf ../clap-$_clap_ref.tar.gz -C clap --strip-components 1
  bsdtar -xf ../clap-helpers-$_clap_helpers_ref.tar.gz -C clap-helpers --strip-components 1
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCLAP_BUILD_TESTS=On \
    -Wno-dev
  cmake --build build --target clap-host clap-tests
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(hicolor-icon-theme librtaudio.so librtmidi.so)
  DESTDIR="$pkgdir" cmake --install build
  cd $pkgname-$pkgver
  install -vDm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
