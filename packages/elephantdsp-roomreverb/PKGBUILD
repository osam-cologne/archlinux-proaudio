# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_plugin_name=RoomReverb
pkgname="elephantdsp-${_plugin_name,,}"
_plugin_uri="https://www.ElephantDSP.com/plugins/Room_Reverb"
pkgver=1.1.0
pkgrel=1
pkgdesc='A mono and stereo algorithmic room reverb audio effect plugin'
arch=(x86_64 aarch64)
url='https://www.elephantdsp.com/products/room-reverb/'
license=(GPL3)
depends=(glibc gcc-libs zlib)
makedepends=(alsa-lib cmake flac freetype2 'juce>=7' libvorbis libx11 libxcursor libxinerama libxrandr)
checkdepends=(lv2lint lv2)
optdepends=(
  'clap-host: for CLAP plugin'
  'lv2-host: for LV2 plugin'
  'vst3-host: for VST3 plugin'
)
groups=(clap-plugins lv2-plugins pro-audio vst3-plugins)
_clap_commit='41964fa3f44fdbf6feb20df528a9cdf2cc3b05ba'
_clap_ext_commit='10bc7d4ddb82eab4796b1ce7d1d2dadd46552f27'
_clap_helpers_commit='41e1086488f5cd27da871d07edb8eccea99e556d'
source=("$pkgname-$pkgver.tar.gz::https://github.com/cvde/RoomReverb/archive/refs/tags/v$pkgver.tar.gz"
        "clap-juce-extensions-$_clap_ext_commit.tar.gz::https://github.com/free-audio/clap-juce-extensions/archive/$_clap_ext_commit.tar.gz"
        "clap-$_clap_commit.tar.gz::https://github.com/free-audio/clap/archive/$_clap_commit.tar.gz"
        "clap-helpers-$_clap_helpers_commit.tar.gz::https://github.com/free-audio/clap-helpers/archive/$_clap_helpers_commit.tar.gz"
        "roomreverb-devendor_juce.patch")
sha256sums=('f31b826e0695c007be61618125e4a14cee59529e8e7047baa1a435b0236bc2c5'
            '76714952d4886f445e7db0d0fc005a9012a5d943e91b5c606a158806beace56d'
            '34bab5c07fddbc2ccc5e5d5177b67abd835a9b9af845f50f154a59622f227891'
            '49b3d5a3813c73ba023e7d3e0702dd70b51706a0e556d9f0639cc18b6f388cfa'
            'a7478a0113128e55fdc14e4b861f95bd65c1c64cc20c0d544bce07f90c0a6ede')

prepare() {
  cd $_plugin_name-$pkgver

  # fix for cmake trying to install into ~/.clap, ~/.lv2, and ~/.vst3 during build
  sed -i -e '/COPY_PLUGIN_AFTER_BUILD/d' CMakeLists.txt

  patch -p1 -N -r - -i "$srcdir"/roomreverb-devendor_juce.patch

  # link external libs
  cd Libs
  rm -rf clap-juce-extensions &&
    ln -s "$srcdir"/clap-juce-extensions-$_clap_ext_commit clap-juce-extensions
  cd clap-juce-extensions/clap-libs
  rm -rf clap &&
    ln -s "$srcdir"/clap-$_clap_commit clap
  rm -rf clap-helpers &&
    ln -s "$srcdir"/clap-helpers-$_clap_helpers_commit clap-helpers
}

build() {
  cmake -S $_plugin_name-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -Wno-dev \
    -B $_plugin_name-$pkgver-build
  cmake --build $_plugin_name-$pkgver-build
}

check() {
  cd $_plugin_name-$pkgver-build
  echo lv2lint -Mpack -q \
    -I "${_plugin_name}_artefacts/Release/LV2/$_plugin_name.lv2/" \
    "$_plugin_uri"
}

package() {
  depends+=(libfreetype.so)
  cd $_plugin_name-$pkgver-build
  # LV2 plugin bundle
  install -Dm755 ${_plugin_name}_artefacts/Release/LV2/$_plugin_name.lv2/*.so \
    -t "$pkgdir"/usr/lib/lv2/$_plugin_name.lv2
  install -Dm644 ${_plugin_name}_artefacts/Release/LV2/$_plugin_name.lv2/*.ttl \
    -t "$pkgdir"/usr/lib/lv2/$_plugin_name.lv2
  # VST3 plugin bundle
  install -Dm755 ${_plugin_name}_artefacts/Release/VST3/$_plugin_name.vst3/Contents/$CARCH-linux/$_plugin_name.so \
    -t "$pkgdir"/usr/lib/vst3/$_plugin_name.vst3/Contents/$CARCH-linux
  # CLAP plugin binary
  install -Dm755 ${_plugin_name}_artefacts/Release/CLAP/$_plugin_name.clap \
    -t "$pkgdir"/usr/lib/clap
}
