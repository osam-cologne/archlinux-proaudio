# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

_slug=Ahornberg
_name=Ahornberg-VCV-Modules
pkgname=vcvrack-ahornberg
pkgver=2.1.6
pkgrel=1
pkgdesc='VCV Rack plugin collection'
arch=(x86_64 aarch64)
url='https://github.com/Ahornberg/Ahornberg-VCV-Modules'
license=(GPL3)
groups=(proaudio vcvrack-plugins)
depends=(vcvrack)
makedepends=(simde zstd)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ahornberg/$_name/archive/refs/tags/v$pkgver.tar.gz"
        "fix-gcc-format.patch::https://github.com/Ahornberg/$_name/pull/20.patch")
sha256sums=('1b7006d7c6bb10c4efc51accf70959384fd512a20a3395104ea181302b79830b'
            'd87e4494844a6c432222487157d81510b402e0b2e55ac0608b1640c71790ab19')

prepare() {
  cd $_name-$pkgver
  # https://github.com/Ahornberg/Ahornberg-VCV-Modules/pull/20
  patch -p1 -i ../fix-gcc-format.patch
}

build() {
  cd $_name-$pkgver
  # vcvrack make flags
  export SLUG=$_slug
  export VERSION=$pkgver
  export RACK_DIR=/usr/share/vcvrack
  make dist
}

package() {
  cd $_name-$pkgver
  install -d "$pkgdir"/usr/lib/vcvrack/plugins
  cp -va dist/$_slug -t "$pkgdir"/usr/lib/vcvrack/plugins
}
