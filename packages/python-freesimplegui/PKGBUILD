# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur at chrisarndt dot de>

_projectname=FreeSimpleGUI
_name=${_projectname,,}
pkgname=python-$_name
pkgver=5.2.0.post1
pkgrel=3
pkgdesc='A free, easy-to-use Python GUI framework based on tkinter'
url='https://github.com/spyoungtech/FreeSimpleGUI'
arch=(any)
license=(LGPL-3.0-or-later)
depends=(python tk)
makedepends=(python-build python-installer python-setuptools)
optdepends=("python-pillow: for taking UI screenshots")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('e58a0e6758e9a9e87152256911f94fcc3998356d1309973a9f4d9df2dc55f98a')

prepare() {
  cd $_name-$pkgver
  # Fix deprecation warnings and possible future build breakage
  # See https://packaging.python.org/en/latest/guides/writing-pyproject-toml/#license
  sed \
    -e 's/^license.*/license = "'${license[0]}'"/' \
    -e '/License ::/d' \
    -i pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
