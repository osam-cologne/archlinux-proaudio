# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Luis Pablo Gasparotto <lpgasparotto at gmail dot com>
# Contributor: Phillip Fynan <pfynan@gmail.com>
# Contributor: Peter Sutton <peter@foxdogstudios.com>

pkgname=abcmidi
pkgver=2023.05.30
pkgrel=1
pkgdesc='A set of tools for converting ABC files to and from MIDI and other related tasks'
arch=(aarch64 x86_64)
url='https://ifdo.ca/~seymour/runabc/top.html'
license=(GPL)
depends=(glibc)
groups=(abc pro-audio)
source=("$pkgname-$pkgver.zip::https://github.com/sshlien/abcmidi/archive/refs/tags/$pkgver.zip")
sha256sums=('4c04abb2f86638a053c1986c32fbce508775364d5a67f37cf987031178d147c6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Install programming documentation to docdir
  install -Dm644 doc/programming/*.txt doc/programming/*.abc \
    -t "$pkgdir"/usr/share/doc/$pkgname/programming
  # Install example ABC files to docdir
  install -Dm644 samples/*.abc -t "$pkgdir"/usr/share/doc/$pkgname/samples
}
