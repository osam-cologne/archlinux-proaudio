# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Luis Pablo Gasparotto <lpgasparotto at gmail dot com>
# Contributor: Phillip Fynan <pfynan@gmail.com>
# Contributor: Peter Sutton <peter@foxdogstudios.com>

pkgname=abcmidi
pkgver=2023.08.22
pkgrel=1
pkgdesc='A set of tools for converting ABC files to and from MIDI and other related tasks'
arch=(aarch64 x86_64)
url='https://ifdo.ca/~seymour/runabc/top.html'
license=(GPL)
depends=(glibc)
groups=(abc pro-audio)
source=("$pkgname-$pkgver.zip::https://github.com/sshlien/abcmidi/archive/refs/tags/$pkgver.zip")
sha256sums=('458e6c46f79bf6270ba89496ae2242d16d44ec0c4de95aa60059701f9942c6e4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Install programming documentation to docdir
  install -Dm644 doc/programming/*.txt doc/programming/*.abc \
    -t "$pkgdir"/usr/share/doc/$pkgname/programming
  # Install example ABC files to docdir
  install -Dm644 samples/*.abc -t "$pkgdir"/usr/share/doc/$pkgname/samples
}
