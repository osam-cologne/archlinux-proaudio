# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Luis Pablo Gasparotto <lpgasparotto at gmail dot com>
# Contributor: Phillip Fynan <pfynan@gmail.com>
# Contributor: Peter Sutton <peter@foxdogstudios.com>

pkgname=abcmidi
pkgver=2022.12.22
pkgrel=1
pkgdesc='A set of tools for converting ABC files to and from MIDI and other related tasks'
arch=(aarch64 x86_64)
url='https://ifdo.ca/~seymour/runabc/top.html'
license=(GPL)
depends=(glibc)
groups=(abc pro-audio)
source=("https://ifdo.ca/~seymour/runabc/abcMIDI-$pkgver.zip")
sha256sums=('008ee7eef72c53bd27d2e46c36c5097af0f0fe23e8de20a028642d8ac06d4557')

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  # Install programming documentation to docdir
  install -Dm644 doc/programming/*.txt doc/programming/*.abc \
    -t "$pkgdir"/usr/share/doc/$pkgname/programming
  # Install example ABC files to docdir
  install -Dm644 samples/*.abc -t "$pkgdir"/usr/share/doc/$pkgname/samples
}
