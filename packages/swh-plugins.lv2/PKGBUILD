# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

_name=lv2
pkgname=swh-plugins.lv2
pkgver=1.0.16
pkgrel=1
pkgdesc="LV2 port of Steve Harris' plugins suite"
arch=(x86_64 aarch64)
url='http://plugin.org.uk/'
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=()
makedepends=(fftw libxslt)
checkdepends=(kxstudio-lv2-extensions lilv lv2lint)
optdepends=('lv2-host: for running the plugins')
provides=(swh-plugins)
source=("$pkgname-$pkgver.tar.gz::https://github.com/swh/$_name/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('bc24512de6e2fb7a493226e2e01a80ba8462a318b15c3b0fd0cd914b018c3548')

build() {
  cd $_name-$pkgver
  make
}

check() {
  _lv2path="$srcdir"/$_name-$pkgver/plugins
  for _plugin in $(LV2_PATH="$_lv2path" lv2ls 2>/dev/null); do
    # several plugins are segfaulting in lv2lint
    LV2_PATH="$_lv2path":/usr/lib/lv2 lv2lint -Mpack $_plugin \
      2>/dev/null || echo "Ignoring error (known to fail)"
  done
}

package() {
  depends+=(libfftw3f.so)
  cd $_name-$pkgver
  make PREFIX="$pkgdir"/usr install-system
}
