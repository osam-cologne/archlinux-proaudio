# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_gitname=ssr
_pkgname=sympathetic-string-resonator
pkgname=$_pkgname-git
pkgdesc='A sympathetic string resonator plugin and JACK application (git version)'
pkgver=r10.e1999da
pkgrel=1
arch=(x86_64)
url='https://github.com/jpcima/ssr'
license=(MIT)
groups=(lv2-plugins pro-audio vst-plugins)
depends=(cairo gcc-libs)
makedepends=(git mesa jack)
optdepends=(
  'jack: stand-alone JACK application'
  'lv2-host: for LV2 plugin'
  'vst-host: for VST plugin'
)
provides=($_gitname.lv2 $_pkgname)
conflicts=($_gitname.lv2 $_gitname.lv2-git)
source=("$_gitname::git+https://github.com/jpcima/$_gitname"
        'dpf::git+https://github.com/DISTRHO/DPF.git'
)
sha256sums=(SKIP SKIP)

pkgver() {
  cd $_gitname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $_gitname
  git submodule init
  git submodule set-url dpf "$srcdir"/dpf
  git -c protocol.file.allow=always  submodule update
}

build() {
  cd $_gitname
  make PREFIX=/usr BUILD_VST2=true
}

package() {
  depends+=(libjack.so)
  cd $_gitname
  make PREFIX=/usr DESTDIR="$pkgdir" BUILD_VST2=true install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
