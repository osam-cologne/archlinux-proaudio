# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_pkgname=sympathetic-string-resonator
pkgname=$_pkgname-git
pkgdesc='A sympathetic string resonator plugin and JACK application (git version)'
pkgver=r15.a6c5a11
pkgrel=1
arch=(x86_64)
url="https://github.com/SpotlightKid/_$pkgname"
license=(BSD-2-Clause BSD-3-Clause ISC MIT OFL-1.1 Zlib)
groups=(lv2-plugins pro-audio vst-plugins)
depends=(gcc-libs glibc libx11)
makedepends=(cairo git libglvnd jack)
optdepends=(
  'jack: for running the stand-alone JACK application'
  'lv2-host: for loading the LV2 format plugin'
  'vst-host: for loading the VST2 format plugin'
)
provides=($_pkgname)
conflicts=($_pkgname)
source=("$_pkgname::git+https://github.com/SpotlightKid/$_pkgname.git#branch=proaudio"
        'dpf::git+https://github.com/DISTRHO/DPF.git'
        'pugl::git+https://github.com/DISTRHO/pugl.git'
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $_pkgname
  git submodule init
  git submodule set-url dpf "$srcdir"/dpf
  git -c protocol.file.allow=always submodule update
  cd dpf
  git submodule init
  git submodule set-url dgl/src/pugl-upstream "$srcdir"/pugl
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $_pkgname
  make PREFIX=/usr BUILD_VST2=true
}

package() {
  depends+=(libcairo.so libjack.so)
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" BUILD_VST2=true install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm644 dpf/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-dpf
  install -Dm644 libs/strings/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-sfizz
  install -Dm644 resources/fonts/liberation/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-liberation
  install -Dm644 thirdparty/blink/COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-blink
  install -Dm644 thirdparty/cpuid/LICENSE.rst \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-cpuid.rst
  install -Dm644 thirdparty/fontstash/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-fontstash.txt
  install -Dm644 thirdparty/stb/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-stb
}
