# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=ykchorus
_plugin_uri="https://chrisarndt.de/plugins/$pkgname"
pkgver=0.2.2
pkgrel=1
pkgdesc='A stereo dual-chorus audio effect LV2, LADSPA and VST2 plugin and JACK application'
arch=(x86_64)
url="https://github.com/SpotlightKid/${pkgname}"
license=(GPL2)
depends=(gcc-libs libglvnd)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=(
  'jack: for stand-alone JACK client program'
  'ladspa-host: for LV2 plugin'
  'lv2-host: for LV2 plugin'
  'vst-host: for VST plugin'
)
groups=(ladspa-plugins lv2-plugins vst-plugins)
source=("https://github.com/SpotlightKid/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver-source.tar.gz")
sha256sums=('c7a4a980d892ba150efe09496f55ee407713a596e4fb7664cc859fdc9ffedcf6')

prepare() {
  cd "$pkgname"
  # We don't want the DSSI version:
  sed -i -e '/ykchorus-dssi/d' Makefile
  sed -i -e '/dssi_dsp$/d' plugins/YKChorus/Makefile
}

build() {
  cd "$pkgname"
  make
}

check() {
  cd "$pkgname"
  lv2lint -Mpack -q -s lv2_generate_ttl -t "Plugin Author Email" \
    -I "bin/$pkgname.lv2/" "$_plugin_uri" ||
    echo "Ignoring lv2lint errors (known to fail)."
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
}
