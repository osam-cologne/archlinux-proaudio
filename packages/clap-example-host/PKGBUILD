# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

_name=clap-host
# clap-host as pkgname is not ideal due to other hosts using that as a virtual provides
pkgname=clap-example-host
pkgver=1.0.3
pkgrel=4
pkgdesc='CLAP example audio plugin host'
arch=(aarch64 x86_64)
url='https://github.com/free-audio/clap-host'
license=(MIT)
depends=(gcc-libs qt6-base)
makedepends=(catch2-v2 cmake git rtaudio rtmidi)
provides=(clap-host)
groups=(pro-audio)
source=("git+https://github.com/free-audio/$_name#tag=$pkgver"
        'git+https://github.com/free-audio/clap'
        'git+https://github.com/free-audio/clap-helpers')
sha256sums=('84b1e4265abc1481bf29075125ffbda179b065635a39fa4971f267aba23590a8'
            'SKIP'
            'SKIP')

prepare() {
  cd $_name
  for _mod in clap clap-helpers; do
    git submodule init $_mod
    git config submodule.$_mod.url "$srcdir/$_mod"
  done
  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -B build-$pkgname -S $_name \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCLAP_BUILD_TESTS=On \
    -DCLAP_HOST_BINARY=clap-example-host \
    -Wno-dev
  cmake --build build-$pkgname --target clap-host clap-tests
}

check() {
  ctest --test-dir build-$pkgname --output-on-failure
}

package() {
  depends+=(hicolor-icon-theme librtaudio.so librtmidi.so)
  DESTDIR="$pkgdir" cmake --install build-$pkgname
  cd $_name
  install -vDm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
