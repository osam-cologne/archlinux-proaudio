# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

_name=clap-host
# clap-host as pkgname is not ideal due to other hosts using that as a virtual provides
pkgname=clap-example-host
pkgver=1.0.3
pkgrel=3
pkgdesc='CLAP example audio plugin host'
arch=(aarch64 x86_64)
url='https://github.com/free-audio/clap-host'
license=(MIT)
depends=(gcc-libs qt6-base)
makedepends=(catch2-v2 cmake rtaudio rtmidi)
provides=(clap-host)
groups=(pro-audio)
_clap_ref='d6cbd4f'
_clap_helpers_ref='15dd627'
source=("$pkgname-$pkgver.tar.gz::https://github.com/free-audio/$_name/archive/refs/tags/$pkgver.tar.gz"
        "clap-$_clap_ref.tar.gz::https://github.com/free-audio/clap/archive/$_clap_ref.tar.gz"
        "clap-helpers-$_clap_helpers_ref.tar.gz::https://github.com/free-audio/clap-helpers/archive/$_clap_helpers_ref.tar.gz")
sha256sums=('03b4a272cde2336024ba34c2b679ab55756e08bc72bc193b8738e9534602b2c2'
            'e5d52790eaccf124725c7511e18204f2caa25e662d853b0d50b1f4baade00d17'
            'd1c035d54743346d0e3735bd4d2b4f12868201b8ab49c716cf1e6a3b39fcf04e')
noextract=(clap-$_clap_ref.tar.gz clap-helpers-$_clap_helpers_ref.tar.gz)

prepare() {
  cd $_name-$pkgver
  bsdtar -xf ../clap-$_clap_ref.tar.gz -C clap --strip-components 1
  bsdtar -xf ../clap-helpers-$_clap_helpers_ref.tar.gz -C clap-helpers --strip-components 1
}

build() {
  cmake -B build-$pkgname -S $_name-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCLAP_BUILD_TESTS=On \
    -DCLAP_HOST_BINARY=clap-example-host \
    -Wno-dev
  cmake --build build-$pkgname --target clap-host clap-tests
}

check() {
  ctest --test-dir build-$pkgname --output-on-failure
}

package() {
  depends+=(hicolor-icon-theme librtaudio.so librtmidi.so)
  DESTDIR="$pkgdir" cmake --install build-$pkgname
  cd $_name-$pkgver
  install -vDm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
